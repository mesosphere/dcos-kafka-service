plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'de.undercouch.download' version '2.1.0'
}
import de.undercouch.gradle.tasks.download.Download
import de.undercouch.gradle.tasks.download.Verify

ext {
  apacheCommonsVer = "1.3"
  curatorTestVer = '2.9.1'
  dropwizardVer = '0.9.2'
  loggingVer = "1.1.1"
  systemRulesVer = "1.16.0"

  downloadJarDir = "https://s3-us-west-2.amazonaws.com/infinity-artifacts/kafka/container-hook"
  statsdClientJar = "java-dogstatsd-client-2.0.13.jar"
  statsdClientJarSha1 = "b86c4ce49c8ea8b20c5a365f44a368d419aaad68"
  kafkaStatsdJar = "kafka-statsd-metrics2-0.4.1.jar"
  kafkaStatsdJarSha1 = "e26a8e097749d53c051a3f580e5448f61e6df816"
}

dependencies {
  compile project(':kafka-commons')
  compile project(':kafka-scheduler')
  compile "org.apache.commons:commons-exec:${apacheCommonsVer}"
  compile "commons-logging:commons-logging:${loggingVer}"
  testCompile project(':kafka-test-commons').sourceSets.test.output
  testCompile "io.dropwizard:dropwizard-testing:${dropwizardVer}"
  testCompile "org.apache.curator:curator-test:${curatorTestVer}"
  testCompile "com.github.stefanbirkner:system-rules:${systemRulesVer}"
}

shadowJar {
  classifier = "uber"

  mergeServiceFiles()

  exclude 'META-INF/*.SF'
  exclude 'META-INF/*.DSA'
  exclude 'META-INF/*.RSA'
}

def downloadDir = new File("${buildDir}/download")
def metricsJarDir = new File("${downloadDir}/metrics-libs")
task createMetricsJarDir {
  metricsJarDir.mkdirs()
}

task downloadStatsdClientJar(dependsOn: createMetricsJarDir, type: Download) {
  src "${downloadJarDir}/${statsdClientJar}"
  onlyIfNewer true
  dest "${metricsJarDir}/${statsdClientJar}"
}
task verifyStatsdClientJar(dependsOn: downloadStatsdClientJar, type: Verify) {
  src new File("${metricsJarDir}/${statsdClientJar}")
  checksum statsdClientJarSha1
  algorithm 'SHA1'
}
// Ensures downloaded jar is available well before distTar/distZip/etc:
jar.dependsOn(verifyStatsdClientJar)

task downloadKafkaStatsdJar(dependsOn: createMetricsJarDir, type: Download) {
  src "${downloadJarDir}/${kafkaStatsdJar}"
  onlyIfNewer true
  dest "${metricsJarDir}/${kafkaStatsdJar}"
}
task verifyKafkaStatsdJar(dependsOn: downloadKafkaStatsdJar, type: Verify) {
  src new File("${metricsJarDir}/${kafkaStatsdJar}")
  checksum kafkaStatsdJarSha1
  algorithm 'SHA1'
}
// Ensures downloaded jar is available well before distTar/distZip/etc:
jar.dependsOn(verifyKafkaStatsdJar)

task wrapper(type: Wrapper) {
  gradleVersion = '2.3'
}

distributions {
  main {
    baseName = 'overrider'
    version = ""
    contents {
      from { downloadDir } // metrics libs go here: "overrider/metrics-libs/*.jar"
    }
  }
}

mainClassName = 'org.apache.mesos.kafka.config.Overrider'